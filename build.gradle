plugins {
    id 'application'
    id 'java'
    id "org.sonarqube" version "2.6.2"
    id "com.github.johnrengelman.shadow" version "4.0.1"
}

group 'be.howest.ti'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    // mavenLocal()
    mavenCentral()
}

mainClassName = 'io.vertx.core.Launcher'

def vertxVersion = '3.6.3'

run {
    args = ['run', mainVerticleName, "--launcher-class=$mainClassName"]
}

shadowJar {
    classifier = 'fat'
    manifest {
        attributes 'Main-Verticle': "$mainVerticleName"
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

configurations {
    htmlValidation
}


dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile "io.vertx:vertx-core:$vertxVersion"
    compile "io.vertx:vertx-web:$vertxVersion"

    // https://groups.google.com/forum/#!topic/vertx/MNb8-tvdp7s
    compile "io.vertx:vertx-codegen:$vertxVersion"

    compile group: 'org.tinylog', name: 'tinylog', version: '1.3.6'
    compile group: 'org.tinylog', name: 'slf4j-binding', version: '1.3.6'

    htmlValidation group: 'nu.validator', name: 'validator', version: '18.11.5'

}


task validateHTML(type: JavaExec) {
    workingDir = 'src/main/resources/webroot'
    main 'nu.validator.client.SimpleCommandLineValidator'
    classpath configurations.htmlValidation
    args '--skip-non-html', './'
}

configure(validateHTML) {
    group = 'verification'
    description = 'Verify and validate HTML'
}

apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.2"
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }
}

check.dependsOn jacocoTestReport
